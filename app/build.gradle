plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

sourceCompatibility = 15
targetCompatibility = 15

group 'com.codelog'
version '1.4.1'

repositories {
    mavenCentral()

    maven {
        url = uri("https://maven.pkg.github.com/Code-Log/clogg-java")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation "org.json:json:20201115"
    implementation "com.google.cloud:google-cloud-storage:1.113.9"

    // CLogg
    implementation 'com.codelog:clogg:1.0.1'

    // Kotlin STDLIB
    implementation('org.jetbrains.kotlin:kotlin-bom')
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation 'com.google.guava:guava:30.1.1-jre'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    api 'org.apache.commons:commons-math3:3.6.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.codelog.schyfts.Main'
    }
}

shadowJar {
    mainClassName = "com.codelog.schyfts.Main"
}

application {
    getMainClass().set("com.codelog.schyfts.Main")
}

task generateJava(type: Copy) {
    def templateContext = [version: project.version]
    inputs.properties(templateContext)
    from 'src/template/java' into "$buildDir/generated/java" expand templateContext
}

sourceSets.main.java.srcDir "$buildDir/generated/java"
compileJava.dependsOn generateJava

release {
    snapshotSuffix = 'ALPHA'
}

testing {
    suites {
        test {
            useKotlinTest()
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "15"
}

