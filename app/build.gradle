plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'net.researchgate.release' version '2.8.1'
}

group 'com.codelog'
version '1.3.1'

repositories {
    mavenCentral()
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation "org.json:json:20201115"
    implementation "com.google.cloud:google-cloud-storage:1.113.9"
    implementation project(":lib")

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.codelog.schyfts.Main'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.codelog.schyfts.Main'
    }
    archiveClassifier.set("all")
    from {
        resources.collect()
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

application {
    getMainClass().set("com.codelog.schyfts.Main")
}

task generateJava(type: Copy) {
    def templateContext = [version: project.version]
    inputs.properties(templateContext)
    from 'src/template/java' into "$buildDir/generated/java" expand templateContext
}

sourceSets.main.java.srcDir "$buildDir/generated/java"
compileJava.dependsOn generateJava

release {
    snapshotSuffix = 'ALPHA'
}